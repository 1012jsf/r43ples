SELECT DISTINCT ?s ?p ?o
WHERE{
    GRAPH <http://eatld.et.tu-dresden.de/r43ples-revisions> {
        # get full graph (?g_full_graph)
        { []  <http://eatld.et.tu-dresden.de/rmo#references>  <http://test.com/r43ples-dataset-1-revision-5> ;
            <http://eatld.et.tu-dresden.de/rmo#fullGraph>  ?g_delete_set_full_graph . }
        UNION
        # get delete set graps (?g_delete_set)
        {?r_delete_set  <http://www.w3.org/1999/02/22-rdf-syntax-ns#type>  <http://eatld.et.tu-dresden.de/rmo#Revision> ;
            <http://eatld.et.tu-dresden.de/rmo#deltaRemoved>  ?g_delete_set_full_graph .
        FILTER ( ?r_delete_set IN (<http://test.com/r43ples-dataset-1-revision-5>,  <http://test.com/r43ples-dataset-1-revision-4>) ) }
    }

    GRAPH ?g_delete_set_full_graph { ?s  ?p  ?o .}
    MINUS { 
        GRAPH ?g_add_set { ?s  ?p  ?o .}
        GRAPH <http://eatld.et.tu-dresden.de/r43ples-revisions> {
            ?r_add_set  <http://www.w3.org/1999/02/22-rdf-syntax-ns#type>  <http://eatld.et.tu-dresden.de/rmo#Revision> ;
                  <http://eatld.et.tu-dresden.de/rmo#deltaAdded>  ?g_add_set .
            FILTER ( ?r_add_set IN (<http://test.com/r43ples-dataset-1-revision-5>,  <http://test.com/r43ples-dataset-1-revision-4>) )
        }
        MINUS{ 
            GRAPH ?g_add_set_old { ?s  ?p  ?o .}
            GRAPH <http://eatld.et.tu-dresden.de/r43ples-revisions> {
                ?r_add_set_old <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> <http://eatld.et.tu-dresden.de/rmo#Revision> .
                ?r_add_set_old <http://eatld.et.tu-dresden.de/rmo#deltaRemoved>|<http://eatld.et.tu-dresden.de/rmo#deltaAdded> ?g_add_set_old .
                ?r_add_set (<http://www.w3.org/ns/prov#wasDerivedFrom>)+ ?r_add_set_old
                FILTER ( ?r_add_set_old IN (<http://test.com/r43ples-dataset-1-revision-5>,  <http://test.com/r43ples-dataset-1-revision-4>) )
            }
        }
    }
}
ORDER BY ?s ?p ?o