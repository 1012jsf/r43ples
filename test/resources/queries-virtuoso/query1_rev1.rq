prefix rmo:	<http://eatld.et.tu-dresden.de/rmo#>
prefix prov:	<http://www.w3.org/ns/prov#> 
prefix eatld:	<http://eatld.et.tu-dresden.de/>

select distinct ?s ?p ?o
where {
	GRAPH ?g1 { ?s ?p ?o. }
  	GRAPH eatld:r43ples-revisions {
  		{ <http://test.com/r43ples-dataset-master> rmo:fullGraph ?g1. } UNION
  		{ ?rg1 a rmo:Revision.
  		  ?rg1 rmo:deltaRemoved ?g1.
          FILTER (?rg1 IN (<http://test.com/r43ples-dataset-revision-4>, <http://test.com/r43ples-dataset-revision-3>, <http://test.com/r43ples-dataset-revision-2>))
        }
    }
  	MINUS {
  		GRAPH eatld:r43ples-revisions{
  			?rm1 a rmo:Revision.
  			?rm1 rmo:deltaAdded ?gm1.
  			FILTER (?rm1 IN (<http://test.com/r43ples-dataset-revision-4>, <http://test.com/r43ples-dataset-revision-3>, <http://test.com/r43ples-dataset-revision-2>))
			OPTIONAL {
				?rm1_old a rmo:Revision.
  				?rm1_old rmo:deltaRemoved ?gm1_old.
  				?rm1 prov:wasDerivedFrom+ ?rm1_old.
  				FILTER (?rm1_old IN (<http://test.com/r43ples-dataset-revision-4>, <http://test.com/r43ples-dataset-revision-3>, <http://test.com/r43ples-dataset-revision-2>))
			}
		}
		GRAPH ?gm1 { ?s ?p ?o }
		MINUS {
			GRAPH ?gm1_old {?s ?p ?o.}	
		}
	}
}